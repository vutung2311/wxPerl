#############################################################################
## Name:        make_ovl_list.pl
## Purpose:     builds overload constants
## Author:      Mattia Barbon
## Modified by:
## Created:     17/ 8/2001
## RCS-ID:      
## Copyright:   (c) 2001 Mattia Barbon
## Licence:     This program is free software; you can redistribute it and/or
##              modify it under the same terms as Perl itself
#############################################################################

use strict;

my $ovl = shift @ARGV;
my $ovlc = shift @ARGV;
my $ovlh = shift @ARGV;

my %name2type =
  (
   wimg => 'Wx::Image',
   wbmp => 'Wx::Bitmap',
   wico => 'Wx::Icon',
   wmen => 'Wx::Menu',
   wmit => 'Wx::MenuItem',
   wrec => 'Wx::Rect',
   wreg => 'Wx::Region',
   wszr => 'Wx::Sizer',
   wtip => 'Wx::ToolTip',
   wwin => 'Wx::Window',
   wcol => 'Wx::Colour',
   wlci => 'Wx::ListItem',
   wsiz => 'Wx::Size',
   wpoi => 'Wx::Point',
   wgco => 'Wx::GridCellCoords',
   wdat => 'Wx::DataObject',
   wcur => 'Wx::Cursor',
   wehd => 'Wx::EvtHandler',
   wist => 1,
   wost => 1,
   num  => 1,
   str  => 1,
   bool => 1,
   arr  => 1,
  );

my %constants;

foreach my $i ( @ARGV ) {
  open IN, '< ' . $i or die "unable to open '$i'";

  while( <IN> ) {
    if( m/Wx::_match\(\s*\@_\s*,\s*\$Wx::_(\w+)\s*\,/ ||
        m/wxPliOvl_(\w+)/ ) {
      my $const = $1;
      my @const = split /_/, $const;
      foreach my $j ( @const ) {
        $j = 'num' if $j eq 'n';
        $j = 'str' if $j eq 's';
        $j = 'bool' if $j eq 'b';

        die "unrecognized type '$j' in file '$i'"
          unless $name2type{$j};
        $constants{$const} = \@const;
      }
    }
  }
}

my @keys = ( ( sort grep { $name2type{$_} != 1 } keys %name2type ),
             ( sort grep { $name2type{$_} == 1 } keys %name2type ) );

my $vars_comma = join ", ", map { "\$$_" } @keys;
my $vars = $vars_comma; $vars =~ s/,//g;
my $types = join ", ", map { "'$name2type{$_}'" }
  grep { $name2type{$_} != 1 } @keys;

=for comment

open OUT, '> '. $ovl || die "unable to open file '$ovl'";
binmode OUT; # Perl 5.004 on Unix complains for CR

print OUT <<EOT;
#############################################################################
## Name:        _Ovl.pm
## Purpose:     overload constants (AUTOGENERATED, DO NOT EDIT)
## Author:      Mattia Barbon
## Modified by:
## Created:     17/ 8/2001
## RCS-ID:      
## Copyright:   (c) 2001 Mattia Barbon
## Licence:     This program is free software; you can redistribute it and/or
##              modify it under the same terms as Perl itself
#############################################################################

package Wx;

EOT

print OUT "use vars qw(\@tnames ${vars});\n";
print OUT "( ${vars_comma} ) = ( 1 .. 100 );\n\n";
print OUT "\@tnames = ( undef, ${types} );\n\n";

foreach my $i ( sort keys %constants ) {
  print OUT "\$Wx::_$i = [ ";
  print OUT join ", ", map { "\$$_" } @{$constants{$i}};
  print OUT " ];\n";
}

print OUT <<EOT;

1;

# Local variables: #
# mode: cperl #
# End: #
EOT

=cut

open OUT, '> '. $ovlh || die "unable to open file '$ovlc'";
binmode OUT;

my $enum = join ",\n", map { "    wxPliOvl$_" } @keys;
my $cpp_types = $types; $cpp_types =~ s/\'/\"/g;

print OUT <<EOT;
// GENERATED FILE, DO NOT EDIT

enum
{
    wxPliOvl_Dummy = 0,
$enum
};

extern const char* wxPliOvl_tnames[];

EOT

foreach my $i ( sort keys %constants ) {
  print OUT "extern const unsigned char wxPliOvl_$i\[\];\n";
  print OUT "#define wxPliOvl_${i}_count " . scalar @{$constants{$i}} . "\n";
}


open OUT, '> '. $ovlc || die "unable to open file '$ovlc'";
binmode OUT;

print OUT <<EOT;
// GENERATED FILE, DO NOT EDIT

const char* wxPliOvl_tnames[] = { 0,
$cpp_types
};

extern void wxPli_set_ovl_constant( const char* name,
                                    const unsigned char* value, int count );
EOT

print OUT <<EOT;
void SetOvlConstants()
{
    dTHX;
    SV* tmp;
EOT

#foreach my $i ( @keys ) {
#  print OUT "    tmp = get_sv( \"Wx::${i}\", 1 ); sv_setiv( tmp, wxPliOvl${i} );\n";
#}

foreach my $i ( keys %constants ) {
  print OUT "    wxPli_set_ovl_constant( \"$i\", wxPliOvl_${i}, wxPliOvl_${i}_count );\n";
}

print OUT "}\n\n";

foreach my $i ( sort keys %constants ) {
  print OUT "const unsigned char wxPliOvl_$i\[\] = { ";
  print OUT join ", ", map { "wxPliOvl$_" } @{$constants{$i}};
  print OUT " };\n";
}

exit 0;

# Local variables: #
# mode: cperl #
# End: #


