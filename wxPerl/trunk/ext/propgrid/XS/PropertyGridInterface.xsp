#############################################################################
## Name:        viface/PropertyGridInterface.xsp
## Purpose:     XS++ for wxPropertyGridInterface
## Author:      Mark Dootson
## Modified by:
## Created:     04/03/2012
## RCS-ID:      $Id:$
## Copyright:   (c) 2012 Mattia Barbon
## Licence:     This program is free software; you can redistribute it and/or
##              modify it under the same terms as Perl itself
#############################################################################

%file{xspp/PropertyGridInterface.h};

%module{Wx};

#if WXPERL_W_VERSION_GE( 2, 9, 3 ) && wxUSE_PROPGRID

%loadplugin{build::Wx::XSP::Overload};

%name{Wx::PGPGridInterfaceBase} class wxPGPGridInterfaceBase
{

public:

    /**

    virtual wxPGVIterator GetVIterator( int flags ) const;
    
    virtual bool IsPropertySelected( wxPGPropArgCls& id ) const;

    **/

    wxPGProperty* Append( wxPGProperty* property );

    wxPGProperty* AppendIn( wxPGPropArgCls& id, wxPGProperty* newProperty );

    void BeginAddChildren( wxPGPropArgCls& id );

    bool ClearSelection( bool validation = false);

    void ClearModifiedStatus();

    bool Collapse( wxPGPropArgCls& id );

    bool CollapseAll();

#ifndef WXPERL_IN_PROPERTYGRID

    bool ChangePropertyValue( wxPGPropArgCls& id, wxVariant newValue );
	
#endif

    void DeleteProperty( wxPGPropArgCls& id );

    bool DisableProperty( wxPGPropArgCls& id );

    bool EditorValidate();

    bool EnableProperty( wxPGPropArgCls& id, bool enable = true );

    void EndAddChildren( wxPGPropArgCls& id );

    bool Expand( wxPGPropArgCls& id );

    bool ExpandAll( bool expand = true );

    int GetColumnProportion( unsigned int column ) const;

    wxPGProperty* GetFirstChild( wxPGPropArgCls& id );

    %name{GetIteratorPropertyOverload} wxPropertyGridIterator GetIterator( int flags = wxPG_ITERATE_DEFAULT,
                                        wxPGProperty* firstProp = NULL ) %Overload;
    /*
    wxPropertyGridConstIterator GetIterator( int flags = wxPG_ITERATE_DEFAULT,
                                             wxPGProperty* firstProp = NULL ) const;
    */
    
    %name{GetIteratorPosOverload} wxPropertyGridIterator GetIterator( int flags, int startPos ) %Overload;
    /*
    wxPropertyGridConstIterator GetIterator( int flags, int startPos ) const;
    */
    
    wxPGProperty* GetFirst( int flags = wxPG_ITERATE_ALL )
        %code{% RETVAL = THIS->wxPropertyGridInterface::GetFirst( flags ); %};

    wxPGProperty* GetProperty( const wxString& name ) const;
/*
    void GetPropertiesWithFlag( wxArrayPGProperty* targetArr,
                                wxByte flags,
                                bool inverse = false,
                                int iterFlags = wxPerl_build_default_propertyflags ) const;
*/

%{

void
wxPGPGridInterfaceBase::GetPropertiesWithFlag(wxByte flags, bool inverse = false, int iterFlags = wxPerl_build_default_propertyflags )
  PREINIT:
    wxArrayPGProperty* targetArr;
  PPCODE:
    THIS->GetPropertiesWithFlag( targetArr, flags, inverse, iterFlags );
    size_t num = targetArr->GetCount();
    EXTEND( SP, (IV)num );
    for( size_t i = 0; i < num; ++i )
    {
        PUSHs( wxPli_object_2_sv( aTHX_ sv_newmortal(), targetArr->Item( i ) ) );
    }    
   
%}

    wxVariant GetPropertyAttribute( wxPGPropArgCls& id, const wxString& attrName ) const;

    wxColour GetPropertyBackgroundColour( wxPGPropArgCls& id ) const;

#if !defined(WXPERL_IN_PROPERTYGRIDPAGE)

    wxPropertyCategory* GetPropertyCategory( wxPGPropArgCls& id ) const;
	wxPGProperty* GetPropertyByLabel( const wxString& label ) const;

#endif

    /** Returns client data (void*) of a property. */
    /*TODO FIXME
    void* GetPropertyClientData( wxPGPropArgCls& id ) const;
    */

    %name{GetPropertyByNameOLName} wxPGProperty* GetPropertyByName( const wxString& name ) const %Overload;

    %name{GetPropertyByNameOLSubName} wxPGProperty* GetPropertyByName( const wxString& name,
                                     const wxString& subname ) const %Overload;

    const wxPGEditor* GetPropertyEditor( wxPGPropArgCls& id ) const;

    wxString GetPropertyHelpString( wxPGPropArgCls& id ) const;

    wxBitmap* GetPropertyImage( wxPGPropArgCls& id ) const;

    const wxString& GetPropertyLabel( wxPGPropArgCls& id );

    wxString GetPropertyName( wxPGProperty* property );

    wxColour GetPropertyTextColour( wxPGPropArgCls& id ) const;

    wxValidator* GetPropertyValidator( wxPGPropArgCls& id );

    wxVariant GetPropertyValue( wxPGPropArgCls& id );

    wxArrayInt GetPropertyValueAsArrayInt( wxPGPropArgCls& id ) const;

    wxArrayString GetPropertyValueAsArrayString( wxPGPropArgCls& id ) const;

    bool GetPropertyValueAsBool( wxPGPropArgCls& id ) const;

    wxDateTime GetPropertyValueAsDateTime( wxPGPropArgCls& id ) const;

    double GetPropertyValueAsDouble( wxPGPropArgCls& id ) const;

    int GetPropertyValueAsInt( wxPGPropArgCls& id ) const;

    long GetPropertyValueAsLong( wxPGPropArgCls& id ) const;

    wxLongLong_t GetPropertyValueAsLongLong( wxPGPropArgCls& id ) const;

    wxString GetPropertyValueAsString( wxPGPropArgCls& id ) const;

    unsigned long GetPropertyValueAsULong( wxPGPropArgCls& id ) const;

    wxULongLong_t GetPropertyValueAsULongLong( wxPGPropArgCls& id ) const;

    wxVariant GetPropertyValues( const wxString& listname = wxEmptyString,
                                 wxPGProperty* baseparent = NULL, long flags = 0 ) const;

    /**
        Returns list of currently selected properties.

        @remarks wxArrayPGProperty should be compatible with std::vector API.
    */
    /* TODO FIXME
    const wxArrayPGProperty& GetSelectedProperties() const;
    */
%{

void
wxPGPGridInterfaceBase::GetSelectedProperties( )
  PPCODE:
    const wxArrayPGProperty& targetArr = THIS->GetSelectedProperties();
    size_t num = targetArr.GetCount();
    EXTEND( SP, (IV)num );
    for( size_t i = 0; i < num; ++i )
    {
        PUSHs( wxPli_object_2_sv( aTHX_ sv_newmortal(), targetArr.Item( i ) ) );
    }    

%}

#if !defined(WXPERL_IN_PROPERTYGRID) && !defined(WXPERL_IN_PROPERTYGRIDMANAGER)

    wxPGProperty* GetSelection() const;

#endif

    bool HideProperty( wxPGPropArgCls& id, bool hide = true, int flags = wxPG_RECURSE );

    static void InitAllTypeHandlers();

    %name{InsertOLProps} wxPGProperty* Insert( wxPGPropArgCls& priorThis, wxPGProperty* newProperty ) %Overload;

    %name{InsertOLIndex} wxPGProperty* Insert( wxPGPropArgCls& parent, int index, wxPGProperty* newProperty ) %Overload;

    bool IsPropertyCategory( wxPGPropArgCls& id ) const;

    bool IsPropertyEnabled( wxPGPropArgCls& id ) const;

    bool IsPropertyExpanded( wxPGPropArgCls& id ) const;

    bool IsPropertyModified( wxPGPropArgCls& id ) const;

    bool IsPropertyShown( wxPGPropArgCls& id ) const;

    bool IsPropertyValueUnspecified( wxPGPropArgCls& id ) const;

    void LimitPropertyEditing( wxPGPropArgCls& id, bool limit = true );

    static void RegisterAdditionalEditors();

    wxPGProperty* RemoveProperty( wxPGPropArgCls& id );

    wxPGProperty* ReplaceProperty( wxPGPropArgCls& id, wxPGProperty* property );

    bool RestoreEditableState( const wxString& src,
                               int restoreStates = 0x3F );

    wxString SaveEditableState( int includedStates = 0x3F ) const;

    static void SetBoolChoices( const wxString& trueChoice,
                                const wxString& falseChoice );

    bool SetColumnProportion( unsigned int column, int proportion );

    void SetPropertyAttribute( wxPGPropArgCls& id, const wxString& attrName,
                               wxVariant value, long argFlags = 0 );

    void SetPropertyAttributeAll( const wxString& attrName, wxVariant value );

    void SetPropertyBackgroundColour( wxPGPropArgCls& id,
                                      const wxColour& colour,
                                      int flags = wxPG_RECURSE );


    void SetPropertyCell( wxPGPropArgCls& id, int column,
                          const wxString& text = wxEmptyString,
                          const wxBitmap& bitmap = wxNullBitmapPtr,
                          const wxColour& fgCol = wxNullColourPtr,
                          const wxColour& bgCol = wxNullColourPtr );

    /* TODO FIXME
    void SetPropertyClientData( wxPGPropArgCls& id, void* clientData );
    */

    void SetPropertyColoursToDefault( wxPGPropArgCls& id );

    void SetPropertyEditor( wxPGPropArgCls& id, const wxPGEditor* editor );

    void SetPropertyEditor( wxPGPropArgCls& id, const wxString& editorName );

    void SetPropertyLabel( wxPGPropArgCls& id, const wxString& newproplabel );

    void SetPropertyName( wxPGPropArgCls& id, const wxString& newName );

    void SetPropertyReadOnly( wxPGPropArgCls& id, bool set = true,
                              int flags = wxPG_RECURSE );

    void SetPropertyValueUnspecified( wxPGPropArgCls& id );
    
    /*
    void SetPropertyValues( const wxVariantList& list,
                            wxPGPropArgCls& defaultCategory = wxNullPropertyPtr );
    */
    
    void SetPropertyValues( const wxVariant& list, wxPGPropArgCls& defaultCategory  );

    void SetPropertyHelpString( wxPGPropArgCls& id, const wxString& helpString );

    void SetPropertyImage( wxPGPropArgCls& id, wxBitmap& bmp );

    bool SetPropertyMaxLength( wxPGPropArgCls& id, int maxLen );

    void SetPropertyTextColour( wxPGPropArgCls& id,
                                const wxColour& colour,
                                int flags = wxPG_RECURSE );

    void SetPropertyValidator( wxPGPropArgCls& id, const wxValidator& validator );

    /** Sets value (integer) of a property. */
    %name{SetPropertyValueAsLong} void SetPropertyValue( wxPGPropArgCls& id, long value );

    /** Sets value (integer) of a property. */
    %name{SetPropertyValueAsInt} void SetPropertyValue( wxPGPropArgCls& id, int value );

    /** Sets value (floating point) of a property. */
    %name{SetPropertyValueAsDouble} void SetPropertyValue( wxPGPropArgCls& id, double value );

    /** Sets value (bool) of a property. */
    %name{SetPropertyValueAsBool} void SetPropertyValue( wxPGPropArgCls& id, bool value );

    /** Sets value (string) of a property. */
    %name{SetPropertyValueAsString} void SetPropertyValue( wxPGPropArgCls& id, const wxString& value );

    /** Sets value (wxArrayString) of a property. */
    %name{SetPropertyValueAsArrayString} void SetPropertyValue( wxPGPropArgCls& id, const wxArrayString& value );

    /** Sets value (wxDateTime) of a property. */
    %name{SetPropertyValueAsDateTime} void SetPropertyValue( wxPGPropArgCls& id, const wxDateTime& value );

    /** Sets value (wxObject*) of a property. */
    %name{SetPropertyValueAsObject} void SetPropertyValue( wxPGPropArgCls& id, wxObject* value );

    /** Sets value (wxObject&) of a property. */
    /** void SetPropertyValue( wxPGPropArgCls& id, wxObject& value ); */

    /** Sets value (native 64-bit int) of a property. */
    %name{SetPropertyValueAsLongLong} void SetPropertyValue( wxPGPropArgCls& id, wxLongLong_t value );

    /** Sets value (native 64-bit unsigned int) of a property. */
    %name{SetPropertyValueAsUnsignedLongLong} void SetPropertyValue( wxPGPropArgCls& id, wxULongLong_t value );

    /** Sets value (wxArrayInt&) of a property. */
    %name{SetPropertyValueAsArrayInt} void SetPropertyValue( wxPGPropArgCls& id, const wxArrayInt& value );

    void SetPropertyValueString( wxPGPropArgCls& id, const wxString& value );

    /**
        Sets value (wxVariant&) of a property.

        @remarks Use wxPropertyGrid::ChangePropertyValue() instead if you need to
                run through validation process and send property change event.
    */
    %name{SetPropertyValueVariant} void SetPropertyValue( wxPGPropArgCls& id, wxVariant value );

    void SetValidationFailureBehavior( int vfbFlags );

    void Sort( int flags = 0 );

    void SortChildren( wxPGPropArgCls& id, int flags = 0 );

    static wxPGEditor* GetEditorByName( const wxString& editorName );
};

#endif

%file{-};


